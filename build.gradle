plugins {
    id 'java'
    id 'java-library'
    id 'io.quarkus'
}

apply from: 'dependencies.gradle'

allprojects {
    group = 'org.tma.intern'
    version = '1.0.0-SNAPSHOT'
    java {
        toolchain {
            languageVersion = JavaLanguageVersion.of(javaVersion.toInteger())
        }
        if (!project.name.contains('adapter')) {
            withSourcesJar()
        }
    }

    repositories {
        mavenCentral()

    }
}

subprojects {
    if (project.name != 'common') {
        apply plugin: 'io.quarkus'
    }

    if (project.name.contains('adapter')) {
        apply plugin: 'java'

        tasks.withType(JavaCompile).configureEach {
            options.encoding = 'UTF-8'
            options.compilerArgs << '-parameters'
        }
    } else {
        apply plugin: 'java-library'
    }

    if (project.name == 'common') {
        tasks.named("jar") {
            enabled = true
        }

    } else {
        tasks.named('test') {
            useJUnitPlatform()
        }

        test {
            systemProperty "java.util.logging.manager", "org.jboss.logmanager.LogManager"
        }
    }

    dependencies {
        implementation enforcedPlatform("${quarkusPlatformGroupId}:${quarkusPlatformArtifactId}:${quarkusPlatformVersion}")
        implementation rootProject.ext.libs.quarkusArc

        testImplementation rootProject.ext.libs.quarkusJunit
    }

}

tasks.named('jar') {
    enabled = false
}

tasks.named('build') {
    enabled = false
}